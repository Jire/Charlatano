/*
 * Charlatano: Free and open-source (FOSS) cheat for CS:GO/CS:CO
 * Copyright (C) 2017 - Thomas G. P. Nappo, Jonathan Beaudoin
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import java.nio.file.Files

buildscript {
	ext.kotlin_version = '1.5.10'
	ext.jnaVersion = '5.8.0'
	ext.gdxVersion = '1.10.0'
	
	repositories {
		jcenter()
		mavenCentral()
	}
	
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id 'java'
	id 'idea'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'kotlin'

group "com.charlatano"
version "1.2.4"

mainClassName = "com.charlatano.Charlatano"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	implementation group: 'org.jire', name: 'kna', version: '0.4.1'
	
	implementation group: 'net.java.dev.jna', name: 'jna', version: jnaVersion
	implementation group: 'net.java.dev.jna', name: 'jna-platform', version: jnaVersion
	
	implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
	
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
	
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-script-runtime', version: kotlin_version
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-compiler-embeddable', version: kotlin_version
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-script-util', version: kotlin_version
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-scripting-compiler-embeddable', version: kotlin_version
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-scripting-jsr223', version: kotlin_version
	
	implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.4'
	
	implementation group: 'net.openhft', name: 'chronicle-core', version: '2.20.127.1'
	implementation "org.slf4j:slf4j-nop:1.7.31"
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

shadowJar {
	archiveBaseName.set('Charlatano')
	archiveClassifier.set(null)
}

task charlatano {
	doLast {
		def name = "Charlatano $version"
		
		def dir = file("build/$name/")
		if (dir.exists()) dir.deleteDir()
		dir.mkdirs()
		
		def jarName = "${name}.jar"
		def jar = file(new File(dir, jarName))
		def allJar = file("build/libs/Charlatano-${version}.jar")
		Files.copy(allJar.toPath(), jar.toPath())
		
		def bat = file(new File(dir, "Start ${name}.bat"))
		Files.write(bat.toPath(), """@echo off
cd /d "%~dp0"
title $name
java -Xmx512m -Xms512m -jar "$jarName"
pause""".getBytes())
		
		Files.copy(file("LICENSE.txt").toPath(), file(new File(dir, "LICENSE.txt")).toPath())
		Files.copy(file("README.md").toPath(), file(new File(dir, "README.md")).toPath())
		
		def dirSettings = file(new File(dir, "settings"))
		dirSettings.mkdirs()
		def settings = file("settings")
		settings.listFiles().each {
			Files.copy(it.toPath(), file(new File(dirSettings, it.getName())).toPath())
		}
	}
}

charlatano.dependsOn shadowJar
charlatano.mustRunAfter shadowJar

sourceSets {
	main.java.srcDirs += 'src/main/java'
	main.java.srcDirs += 'src/main/kotlin'
	main.java.srcDirs += 'settings'
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
		freeCompilerArgs = ["-Xinline-classes"]
	}
}