/*
 * Charlatano: Free and open-source (FOSS) cheat for CS:GO/CS:CO
 * Copyright (C) 2017 - Thomas G. P. Nappo, Jonathan Beaudoin
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import java.nio.file.Files

buildscript {
	ext.kotlin_version = '1.1.4-2'
	ext.jnaVersion = '4.4.0'
	ext.gdxVersion = '1.9.6'
	
	repositories {
		mavenCentral()
		jcenter()
	}
	
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
	}
}

apply plugin: "idea"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "com.github.johnrengelman.shadow"

group "com.charlatano"
version "1.0.6"

mainClassName = "com.charlatano.Charlatano"

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-util', version: kotlin_version
	
	compile group: "org.jire.arrowhead", name: "arrowhead", version: "1.3.3"
	
	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
	
	compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
	
	compile group: 'net.java.dev.jna', name: 'jna', version: jnaVersion
	compile group: 'net.java.dev.jna', name: 'jna-platform', version: jnaVersion
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

shadowJar {
	baseName = 'Charlatano'
	classifier = null
}

task charlatano {
	doLast {
		def name = "Charlatano $version"
		
		def dir = file("build/$name/")
		if (dir.exists()) dir.deleteDir()
		dir.mkdirs()
		
		def jarName = "${name}.jar"
		def jar = file(new File(dir, jarName))
		def allJar = file("build/libs/Charlatano-${version}.jar")
		Files.copy(allJar.toPath(), jar.toPath())
		
		def bat = file(new File(dir, "@start.bat"))
		Files.write(bat.toPath(), """@echo off
title %random%%random%%random%%random%%random%%random%%random%%random%%random%%random%%random%%random%
echo Only continue once you are in game and can see your gun.
cd /d "%~dp0"
set name=Char%random%%random%
ren "Char*.jar" "%name%.jar"
set args=-server -Xmn128M -Xms256M -Xmx512M

:: If you are having trouble remove any lines below that are causing problems
set args=%args% -XX:+UseStringDeduplication
set args=%args% -XX:+UseFastAccessorMethods
set args=%args% -XX:+UseG1GC
set args=%args% -XX:+OptimizeFill
set args=%args% -XX:+UseFastEmptyMethods
set args=%args% -XX:+EliminateLocks
set args=%args% -XX:+AggressiveOpts
set args=%args% -XX:+DoEscapeAnalysis
set args=%args% -XX:+UseCompressedOops
:: If you are having trouble remove any lines above that are causing problems

java %args% -jar "%name%.jar"
pause""".getBytes())
		
		Files.copy(file("LICENSE.txt").toPath(), file(new File(dir, "LICENSE.txt")).toPath())
		Files.copy(file("README.md").toPath(), file(new File(dir, "README.md")).toPath())
		
		def dirSettings = file(new File(dir, "settings"))
		dirSettings.mkdirs()
		def settings = file("settings")
		settings.listFiles().each {
			Files.copy(it.toPath(), file(new File(dirSettings, it.getName())).toPath())
		}
		
		Files.copy(file(project.configurations.compile.find {
			it.name.startsWith("kotlin-compiler-${kotlin_version}.jar")
		}).toPath(), file(new File(dir, "kotlin-compiler.jar")).toPath())
	}
}

charlatano.dependsOn shadowJar
charlatano.mustRunAfter shadowJar

sourceSets {
	main.java.srcDirs += 'src/main/java'
	main.java.srcDirs += 'src/main/kotlin'
	main.java.srcDirs += 'settings'
}
