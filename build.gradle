/*
 *     Charlatano: Free and open-source (FOSS) cheat for CS:GO/CS:CO
 *     Copyright (C) 2017 - Thomas G. P. Nappo, Jonathan Beaudoin
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Affero General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Affero General Public License for more details.
 *
 *     You should have received a copy of the GNU Affero General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import java.nio.file.Files

buildscript {
	ext.kotlin_version = '1.1.0-beta-38'
	ext.kotlin_eap_repo = 'https://dl.bintray.com/kotlin/kotlin-eap-1.1'
	
	ext.gdxVersion = '1.9.5'
	
	repositories {
		mavenCentral()
		maven { url kotlin_eap_repo }
	}
	
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id "us.kirchmeier.capsule" version "1.0.2"
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "application"

group "com.charlatano"
version "0.9.6"

mainClassName = "com.charlatano.Charlatano"

repositories {
	mavenCentral()
	jcenter()
	maven { url kotlin_eap_repo }
}

dependencies {
	compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: kotlin_version
	compile group: 'org.jetbrains.kotlin', name: 'kotlin-script-util', version: kotlin_version
	
	compile group: "org.jire.arrowhead", name: "arrowhead", version: "1.2.4"
	
	compile group: "it.unimi.dsi", name: "fastutil", version: "7.0.13"
	
	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
	
	compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
	compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
	
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.3.0'
	compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.3.0'
}

task mavenCapsule(type: MavenCapsule) {
	applicationClass mainClassName
	reallyExecutable
	capsuleManifest {
		allowSnapshots = true
		delegate.repositories = ["https://repo1.maven.org/maven2/",
		                         "http://dl.bintray.com/jire/maven/",
		                         "https://dl.bintray.com/kotlin/kotlin-eap-1.1/"]
		jvmArgs = ["-Xskip-runtime-version-check"]
	}
}

task charlatano {
	doLast {
		def name = "Charlatano $version"
		
		def dir = file("build/$name/")
		if (dir.exists()) dir.deleteDir()
		dir.mkdirs()
		
		def jarName = "${name}.jar"
		def jar = file(new File(dir, jarName))
		def capsule = file("build/libs/Charlatano-${version}-capsule.jar")
		Files.copy(capsule.toPath(), jar.toPath())
		
		def bat = file(new File(dir, "Start ${name}.bat"))
		Files.write(bat.toPath(), """@echo off
title $name
java -jar "$jarName"
pause""".getBytes())
		
		Files.copy(file("LICENSE.txt").toPath(), file(new File(dir, "LICENSE.txt")).toPath())
		Files.copy(file("README.md").toPath(), file(new File(dir, "README.md")).toPath())
		
		def dirSettings = file(new File(dir, "settings"))
		dirSettings.mkdirs()
		def settings = file("settings")
		settings.listFiles().each {
			Files.copy(it.toPath(), file(new File(dirSettings, it.getName())).toPath())
		}
	}
}

charlatano.dependsOn mavenCapsule
charlatano.mustRunAfter mavenCapsule

sourceSets {
	main.java.srcDirs += 'src/main/kotlin'
}